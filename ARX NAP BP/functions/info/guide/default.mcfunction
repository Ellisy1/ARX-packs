# Проводим самостоятельный (от лица скрипта) сбор нужных данных
    # Навыки
        # A
            execute as @s[scores={sk_a_1=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_2=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_3=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_4=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_5=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_6=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_7=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_8=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_9=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_10=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_11=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_12=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_13=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_14=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_15=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_16=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_17=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_18=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_19=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_20=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_21=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_22=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_23=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_24=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_25=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_26=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_27=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_28=0}] run tag @s add has_unimproved_a_skill
            execute as @s[scores={sk_a_29=0}] run tag @s add has_unimproved_a_skill
        # B
            execute as @s[scores={sk_b_1=0}] run tag @s add has_unimproved_b_skill
            execute as @s[scores={sk_b_2=0}] run tag @s add has_unimproved_b_skill
            execute as @s[scores={sk_b_3=0}] run tag @s add has_unimproved_b_skill
            execute as @s[scores={sk_b_4=0}] run tag @s add has_unimproved_b_skill
            execute as @s[scores={sk_b_5=0}] run tag @s add has_unimproved_b_skill
            execute as @s[scores={sk_b_6=0}] run tag @s add has_unimproved_b_skill
            execute as @s[scores={sk_b_7=0}] run tag @s add has_unimproved_b_skill
            execute as @s[scores={sk_b_8=0}] run tag @s add has_unimproved_b_skill
            execute as @s[scores={sk_b_9=0}] run tag @s add has_unimproved_b_skill
            execute as @s[scores={sk_b_10=0}] run tag @s add has_unimproved_b_skill
            execute as @s[scores={sk_b_11=0}] run tag @s add has_unimproved_b_skill
            execute as @s[scores={sk_b_12=0}] run tag @s add has_unimproved_b_skill
            execute as @s[scores={sk_b_13=0}] run tag @s add has_unimproved_b_skill
            execute as @s[scores={sk_b_14=0}] run tag @s add has_unimproved_b_skill
        # C
            execute as @s[scores={sk_c_1=0}] run tag @s add has_unimproved_c_skill
            execute as @s[scores={sk_c_2=0}] run tag @s add has_unimproved_c_skill
            execute as @s[scores={sk_c_3=0}] run tag @s add has_unimproved_c_skill
            execute as @s[scores={sk_c_4=0}] run tag @s add has_unimproved_c_skill
            execute as @s[scores={sk_c_5=0}] run tag @s add has_unimproved_c_skill
            execute as @s[scores={sk_c_6=0}] run tag @s add has_unimproved_c_skill
            execute as @s[scores={sk_c_7=0}] run tag @s add has_unimproved_c_skill
            execute as @s[scores={sk_c_8=0}] run tag @s add has_unimproved_c_skill
            execute as @s[scores={sk_c_9=0}] run tag @s add has_unimproved_c_skill
            execute as @s[scores={sk_c_10=0}] run tag @s add has_unimproved_c_skill
            execute as @s[scores={sk_c_11=0}] run tag @s add has_unimproved_c_skill
            execute as @s[scores={sk_c_12=0}] run tag @s add has_unimproved_c_skill
            execute as @s[scores={sk_c_13=0}] run tag @s add has_unimproved_c_skill
            execute as @s[scores={sk_c_14=0}] run tag @s add has_unimproved_c_skill
            execute as @s[scores={sk_c_15=0}] run tag @s add has_unimproved_c_skill
        # D
            execute as @s[scores={sk_d_1=0}] run tag @s add has_unimproved_d_skill
            execute as @s[scores={sk_d_2=0}] run tag @s add has_unimproved_d_skill
            execute as @s[scores={sk_d_3=0}] run tag @s add has_unimproved_d_skill
            execute as @s[scores={sk_d_4=0}] run tag @s add has_unimproved_d_skill

    # Опыт майна
        execute as @s[lm=30] run tag @s add level_is_more_or_equal_30
        execute as @s[lm=40] run tag @s add level_is_more_or_equal_40
        execute as @s[lm=50] run tag @s add level_is_more_or_equal_50
        execute as @s[lm=60] run tag @s add level_is_more_or_equal_60
        execute as @s[lm=70] run tag @s add level_is_more_or_equal_70

# Даем советы
    # Нокаут
        execute as @s[scores={respawn_delay=1..}] run tellraw @s { "rawtext": [ { "text": "§cВы в нокауте. Ничего страшного, это не смерть. Вы встанете через минуту - полторы после нока, но вам могут помочь ваши друзья, если они присядут (встанут на шифт) рядом с вами." } ] }
        execute as @s[scores={respawn_delay=1..}] run tag @s add has_smth_to_guide

    # Ранения
        # very_low_hp - очень сильные ранения
            execute as @s[tag=very_low_hp, hasitem={item=arx:scroll_of_healing}, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§cВы очень сильно ранены!§6\nВылечитесь как можно быстрее, иначе у вас повысится стресс!\nИспользуйте §dсвиток регенерации§6, который у вас есть." } ] }
            execute as @s[tag=very_low_hp, hasitem={item=arx:scroll_of_healing}, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide

            execute as @s[tag=very_low_hp, scores={saturation=0}, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§cВы очень сильно ранены!§6\nВылечитесь как можно быстрее, иначе у вас повысится стресс!\nПоешьте что-то вкусное, вы как раз проголодались." } ] }
            execute as @s[tag=very_low_hp, scores={saturation=0}, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide

            execute as @s[tag=very_low_hp, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§cВы очень сильно ранены!§6\nВылечитесь как можно быстрее, иначе у вас очень сильно повысится стресс!\nВылечиться можно при помощи вкусной еды, свитка или магии." } ] }
            execute as @s[tag=very_low_hp, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide

        # low_hp - неприятные ранения
            execute as @s[tag=low_hp, hasitem={item=arx:scroll_of_healing}, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§cВы ранены!§6\nВылечитесь как можно быстрее, иначе у вас повысится стресс!\nИспользуйте §dсвиток регенерации§6, который у вас есть." } ] }
            execute as @s[tag=low_hp, hasitem={item=arx:scroll_of_healing}, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide

            execute as @s[tag=low_hp, scores={saturation=0}, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§cВы ранены!§6\nВылечитесь как можно быстрее, иначе у вас повысится стресс!\nПоешьте что-то вкусное, вы как раз проголодались." } ] }
            execute as @s[tag=low_hp, scores={saturation=0}, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide

            execute as @s[tag=low_hp, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§cВы ранены!§6\nВылечитесь как можно быстрее, иначе у вас повысится стресс!\nВылечиться можно при помощи вкусной еды, свитка или магии." } ] }
            execute as @s[tag=low_hp, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide

    # Стресс
        # 2 ур.
            execute as @s[scores={stress_cond=2, saturation=0}, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§cУ вас 2 ур. стресса.§6\nВкусно покушайте, и он пройдет, вы как раз проголодались." } ] }
            execute as @s[scores={stress_cond=2, saturation=0}, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide

            execute as @s[scores={stress_cond=2}, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§cУ вас 2 ур. стресса.§6\nВкусно покушайте, сделайте и используйте свиток забвения или просто подождите, пока он не пройдет. Подробные дебаффы от 2-го ур. стресса вы можете посмотреть с помощью предмета <Инфо> во вкладке <Общие данные о персонаже>." } ] }
            execute as @s[scores={stress_cond=2}, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide
        
        # 3 ур.
            execute as @s[scores={stress_cond=3, saturation=0}, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§cУ вас 3 ур. стресса.§6\nВкусно покушайте, и вам станет лучше, вы как раз проголодались. §oТолько ради бога, не ешьте кору..." } ] }
            execute as @s[scores={stress_cond=3, saturation=0}, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide

            execute as @s[scores={stress_cond=3}, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§cУ вас 3 ур. стресса.§6\nВкусно покушайте, сделайте и используйте свиток забвения или просто подождите, пока он не пройдет. Подробные дебаффы от 3-го ур. стресса вы можете посмотреть с помощью предмета <Инфо> во вкладке <Общие данные о персонаже>." } ] }
            execute as @s[scores={stress_cond=3}, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide
        
        # 4 ур.
            execute as @s[scores={stress_cond=4}, hasitem={item=arx:scroll_of_oblivion}, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§cУ вас 4, максимальный ур. стресса.§6\nСамое время использовать §dсвиток забвения§6, чтобы его сбросить." } ] }
            execute as @s[scores={stress_cond=4}, hasitem={item=arx:scroll_of_oblivion}, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide

            execute as @s[scores={stress_cond=4, saturation=0}, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§cУ вас 4, максимальный ур. стресса.§6\nВкусно покушайте, и вам полегчает, вы как раз проголодались." } ] }
            execute as @s[scores={stress_cond=4, saturation=0}, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide

            execute as @s[scores={stress_cond=4}, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§cУ вас 4, максимальный ур. стресса.§6\nВкусно покушайте, сделайте и используйте свиток забвения или просто подождите, пока он не пройдет. Подробные дебаффы от 4-го ур. стресса вы можете посмотреть с помощью предмета <Инфо> во вкладке <Общие данные о персонаже>. §cНе забывайте, что при 4 ур. стресса можно получить случайную черту хараектра, при чем более вероятно отрицательную." } ] }
            execute as @s[scores={stress_cond=4}, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide


    # Скрафтить верстак
        execute as @s[scores={ach_has_crafting_table=0}, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§aНачните с создания вестака!§6 Для этого:\n§a1.§6 Добудьте рукой маленькие камушки. Их можно найти везде на поверхности\n§a2.§6 Зайдите в инвентарь и сделайте из них два блока булыжника\n§a3.§6 Добудьте дерево и скрафтите доски. Пожалуйста, дорубайте деревья целиком!\n§a4.§6 Скрафтите верстак и поставьте его!" } ] }
        execute as @s[scores={ach_has_crafting_table=0}, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide
    
    # Невложенные очки обучения
        execute as @s[scores={skill_point=1..}, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§aУ вас есть §d" }, { "score": {"name": "@s", "objective": "skill_point" } }, { "text": " §aневкачанное(-ых) очко(-в) обучения.§6 Чтобы их использовать, создайте и используйте один из трех предметов на обычном вестаке: Повысить силу, Повысить ловкость или Повысить магию." } ] }
        execute as @s[scores={skill_point=1..}, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide

    # Еда
        execute as @s[scores={saturation=0}, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§cПочему бы не перекусить?§6\nПокушайте. Помните, чем вкуснее для вас еда, тем больше она вам даст счастья и лечения." } ] }
        execute as @s[scores={saturation=0}, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide

    # Повышение религии 
        execute as @s[scores={religion=0}, tag=level_is_more_or_equal_30, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§aВы можете повысить религию до уровня Ученик! §6Скрафтите на обычном верстаке либо каменного Пиреса, либо каменного Палласа, и используйте." } ] }
        execute as @s[scores={religion=0}, tag=level_is_more_or_equal_30, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide


    # Есть доступные d-навыки
        execute as @s[tag=has_unimproved_d_skill, tag=level_is_more_or_equal_60, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§aВы можете вкачать D навык! §6У вас как раз хватает опыта, и вы можете его вкачать с помощью книжки. Чтобы скрафтить книжки для D навыков, создайте на обычном верстаке набор D-навыков и используйте его." } ] }
        execute as @s[tag=has_unimproved_d_skill, tag=level_is_more_or_equal_60, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide

    # Есть доступные c-навыки
        execute as @s[tag=has_unimproved_c_skill, tag=level_is_more_or_equal_50, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§aВы можете вкачать C навык! §6У вас как раз хватает опыта, и вы можете его вкачать с помощью книжки. Чтобы скрафтить книжки для C навыков, создайте на обычном верстаке набор C-навыков и используйте его." } ] }
        execute as @s[tag=has_unimproved_c_skill, tag=level_is_more_or_equal_50, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide

    # Есть доступные b-навыки
        execute as @s[tag=has_unimproved_b_skill, tag=level_is_more_or_equal_40, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§aВы можете вкачать B навык! §6У вас как раз хватает опыта, и вы можете его вкачать с помощью книжки. Чтобы скрафтить книжки для B навыков, создайте на обычном верстаке набор B-навыков и используйте его." } ] }
        execute as @s[tag=has_unimproved_b_skill, tag=level_is_more_or_equal_40, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide

    # Есть доступные a-навыки
        execute as @s[tag=has_unimproved_a_skill, tag=level_is_more_or_equal_30, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§aВы можете вкачать A навык! §6У вас как раз хватает опыта, и вы можете его вкачать с помощью книжки. Чтобы скрафтить книжки для А навыков, создайте на обычном верстаке набор А-навыков и используйте его." } ] }
        execute as @s[tag=has_unimproved_a_skill, tag=level_is_more_or_equal_30, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide

    # Есть доступная религия
        # 1 ур
            execute as @s[scores={religion=0}, tag=!level_is_more_or_equal_30, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§aВы сможете повысить религию до уровня Ученик, если у вас будет 30-й уровень! §6Это даст приятные бонусы." } ] }
            execute as @s[scores={religion=0}, tag=!level_is_more_or_equal_30] run tag @s add has_smth_to_guide

    # Уровни Аркса
        execute as @s[scores={xp_stage=0..3}, tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§aВам стоит повысить уровень Аркса! §6У вас сейчас §a" }, { "score": {"name": "@s", "objective": "xp" } }, { "text": "XP§6, а нужно §a" }, { "score": {"name": "@s", "objective": "xp_lim" } }, { "text": "XP§6. Чтобы повысить уровень, попробуйте добыть осколки опыта в шахте, поделать еду, получить достижения или поохотиться §a(с последним осторожно, мобы в Арксе довольно опасные)" } ] }
        execute as @s[scores={xp_stage=0..3}, tag=!has_smth_to_guide] run tag @s add has_smth_to_guide


    # Нечего советовать
        execute as @s[tag=!has_smth_to_guide] run tellraw @s { "rawtext": [ { "text": "§aВ настоящее время гиду нечего сказать. Просто продолжайте играть!" } ] }

# Чистим теги
    tag @s remove has_smth_to_guide

    tag @s remove has_unimproved_a_skill
    tag @s remove has_unimproved_b_skill
    tag @s remove has_unimproved_c_skill
    tag @s remove has_unimproved_d_skill

    tag @s remove level_is_more_or_equal_30
    tag @s remove level_is_more_or_equal_40
    tag @s remove level_is_more_or_equal_50
    tag @s remove level_is_more_or_equal_60
    tag @s remove level_is_more_or_equal_70